model{
  # C = number of centers
	# nc[i] = number of subjects in center i
  # the mean of center i is given in theta[i] of length 8
	# tau is 1/sigma^2
	
	# likelihood - level 1
      for (j in 1:N){
	      chol[j] ~ dnorm(theta[cluster[j]],tau)
	    }
	 
	# the distribution of the thetas - level 2
      for (i in 1:C){
	      theta[i] ~ dnorm(mu,tau.theta) 
	      B[i] <- tau.theta/(tau.theta+ nc[i]*tau)
      }
	    
	# prediction for cluster 1
	    for (j in 1:nc[1]){
	    predict[j] ~ dnorm(theta[cluster[j]],tau)
	    }
	    mpredict<-mean(predict[])
		
  # prior for sigma2
		  tau ~ dgamma(0.001,0.001)
		  sigma <- 1 / sqrt(tau)
		  sigma.2 <- 1/tau
		
  # prior for mu
	  	mu ~ dnorm(0.0,1.0E-6)	 
		
	# prior for sigma.theta
		  sigma.theta ~ dunif(0,100)
		  sigma.theta2 <- pow(sigma.theta,2)
		  tau.theta<- pow(sigma.theta,-2)
		
	# intra-class correlation
		  r <- sigma.theta2/(sigma.theta2+sigma.2)
		
	# prediction new observations of a new subsidiary
		
		  theta.new ~ dnorm(mu,tau.theta)
		  for (j in 1:nnew){  
	      	predict.new[j] ~ dnorm(theta.new,tau)
			 }
		  mpredict.new <- mean(predict.new[])
  }